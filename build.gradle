buildscript {
    ext.kotlin_version = '1.4.10'
    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

repositories {
    jcenter()
    google()
}

allprojects {
    repositories {
        jcenter()
        google()
    }
}

dependencies {
    //    compile ('com.mapbox.mapboxsdk:mapbox-android-sdk:0.5.1@aar'){
    //        transitive=true
    //    }
    //    compile ('com.cocoahero.android:geojson:1.0.0@aar'){
    //        transitive=true
    //    }
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'eu.chainfire:libsuperuser:1.0.0.+'
    implementation 'com.google.android.material:material:1.2.1'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'androidx.fragment:fragment-ktx:1.2.5'
    implementation 'androidx.localbroadcastmanager:localbroadcastmanager:1.0.0'
}

ext {
    // Include the default language and base languages for runtime use
    supportedLocales = ['en-rUS', 'fr']
    // Convert Android locale qualifier values to standard identifiers
    // - Remove the 'r' before the region
    // - Remove the 'b+' prefix on BPC 47 tags
    // - Change '+' to '-' in BPC 47 tags
    resToLoc = { res -> res.replaceAll(/-r/, '-').replaceAll(/^b\+/, '').replaceAll(/\+/, '-') }
}

android {
    compileSdkVersion 30
    buildToolsVersion '30.0.2'

    compileOptions {
        sourceCompatibility '1.8'
        targetCompatibility '1.8'
    }

    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 30
        versionCode 12
        vectorDrawables.useSupportLibrary = true
        resConfigs supportedLocales
        buildConfigField "String[]", "LOCALES", '{"' + supportedLocales.collect { resToLoc(it) }.join('","') + '"}'
    }
    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }

        // instrumentTest.setRoot('tests')
    }
}

androidExtensions {
    experimental = true
}